package analizadores;
import java_cup.runtime.*;

/*
    Terminales
*/
terminal String PTCOMA,PARIZQ,PARDER,CORIZQ,CORDER;
terminal String MAS,MENOS,POR,DIVIDIDO;
terminal String ENTERO;
terminal String DECIMAL;
terminal String UMENOS;
terminal String REVALUAR;

/*
    No terminales
*/

non terminal ini;
non terminal instrucciones;
non terminal instruccion;
non terminal Double expresion;

/*
    Precedencia de operadores
*/

precedence left MAS,MENOS;
precedence left POR,DIVIDIDO;
precedence right UMENOS; 

start with ini; 

ini ::= instrucciones;

instrucciones ::= 
   instruccion instrucciones
 | instruccion
;

instruccion ::= 
   REVALUAR CORIZQ expresion:a CORDER PTCOMA {:System.out.println("El valor de la expresion es: "+a);:}
;

expresion  ::= 
    MENOS expresion:a                      {:RESULT=a*-1;:}%prec UMENOS     
 |  expresion:a MAS        expresion:b     {:RESULT=a+b;:}
 |  expresion:a MENOS      expresion:b     {:RESULT=a-b;:}
 |  expresion:a POR        expresion:b     {:RESULT=a*b;:}
 |  expresion:a DIVIDIDO   expresion:b     {:RESULT=a/b;:}
 |  ENTERO:a                               {:RESULT=new Double(a);:}
 |  DECIMAL:a                              {:RESULT=new Double(a);:}
 |  PARIZQ expresion:a PARDER              {:RESULT=a;:}
;
